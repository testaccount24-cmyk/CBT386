*---------------------------------
* HOUSEKEEPING
*---------------------------------
IRXF@DAI CSECT
IRXF@DAI AMODE 31
IRXF@DAI RMODE 24
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R2,R1
         USING IRXF@DAI,R12
         LA    R13,SAVER
         ST    R0,STKENV
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
         LOAD  EP=IRXSTK
         LR    R8,R0
*---------------------------------
* 1st parm
*---------------------------------
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         LTR   R6,R06
         BZ    FINNOK
         C     R6,=F'7'
         BL    FINNOK
         XC    STATUS,STATUS
         CLI   0(R5),C'Y'
         BNE   PARM2
         OI    STATUS,STATDEV
PARM2    CLI   1(R5),C'Y'
         BNE   PARM3
         OI    STATUS,STATSPA
PARM3    CLI   2(R5),C'Y'
         BNE   PARM4
         OI    STATUS,STATSMS
PARM4    CLI   3(R5),C'Y'
         BNE   PARM5
         OI    STATUS,INCOFF
PARM5    CLI   4(R5),C'Y'
         BNE   PARM6
         OI    STATUS,INCNSMS
PARM6    CLI   5(R5),C'Y'
         BNE   PARM7
         OI    STATUS,INCSMS
PARM7    CLI   6(R5),C'Y'
         BNE   BEGSCAN
         OI    STATUS,MONITOR
*---------------------------------
* MAIN
*---------------------------------
BEGSCAN  EQU   *
         TM    STATUS,MONITOR
         BZ    BEGSCAN1
         XC    WORKA,WORKA
         LA    R0,1
         LA    R15,200
         SVC   109
         LTR   R15,R15
         BNZ   FINNOK
         SR    R6,R6
SCANP    UCBSCAN ADDRESS,WORKAREA=WORKA,UCBPTR=UCBPTR,DEVCLASS=DASD,   X
               DYNAMIC=YES,RANGE=ALL,NOPIN
         C     R15,=F'5'
         BNL   FINNOK
         LTR   R15,R15
         BNZ   ENDSCANP
         LA    R6,1(R6)
         B     SCANP
ENDSCANP ST    R6,N
         MVC   I,=F'1'
         XC    W,W
         MVC   T,=CL30'Reading Volumes'
         LA    R1,W1
         CALL  IRXFMONI
BEGSCAN1 MVC   STKFUNC,KQUEUE
         LA    R1,TIPO
         ST    R1,STKDAT
         XC    WORKA,WORKA
         LA    R0,1
         LA    R15,200
         SVC   109
         LTR   R15,R15
         BNZ   FINNOK
         SR    R10,R10
SCAN     UCBSCAN ADDRESS,WORKAREA=WORKA,UCBPTR=UCBPTR,DEVCLASS=DASD,   X
               DYNAMIC=YES,RANGE=ALL,NOPIN
         C     R15,=F'5'
         BNL   FINNOK
         LTR   R15,R15
         BNZ   ENDSCAN
*
         TM    STATUS,MONITOR
         BZ    NOMONI
         LA    R10,1(R10)
         ST    R10,I
         LA    R1,W1
         CALL  IRXFMONI
NOMONI   L     R3,UCBPTR
         S     R3,=AL4(UCBPRFX)
         USING UCB,R3
         UCBDEVN UCBPTR=UCBPTR,DEVN=ADDR
         MVC   VOLSER,UCBVOLI
         TM    UCBSTAT,UCBONLI
         BO    INC1
         TM    STATUS,INCOFF
         BZ    SCAN
         B     BEGPROC
INC1     TM    UCBFL5,UCBSMS
         BO    INC2
         TM    STATUS,INCNSMS
         BZ    SCAN
         B     BEGPROC
INC2     TM    UCBFL5,UCBSMS
         BZ    BEGPROC
         TM    STATUS,INCSMS
         BZ    SCAN
BEGPROC  TM    UCBSTAT,UCBONLI
         BZ    OFFLINE
         TM    STATUS,STATDEV
         BZ    LSPAC
         DEVTYPE ,(RTNDAT,24),UCBLIST=(UCBPTR,1)
LSPAC    TM    STATUS,STATSPA
         BZ    VSMS
         LSPACE DATA=RTNDATL,UCB=UCBPTR
VSMS     TM    UCBFL5,UCBSMS
         BZ    NOSMS
*---------------------------------------------------------------------*
         TM    STATUS,STATSMS
         BZ    NEXT
         XC    SSOB_WRK,SSOB_WRK
         LA    R6,SSOB_WRK             POINT TO WORK AREA
         USING SSOB,R6
         MVI   SSOBLEN+1,SSOBHSIZ      SSOB LENGTH
         MVC   SSOBID,=C'SSOB'         SSOB
         MVI   SSOBFUNC+1,SSOBSSMS     FUNCTION
         L     R1,CVTPTR               CVT
         L     R1,CVTJESCT-CVTMAP(,R1) JES COMM TABLE
         L     R1,JESCTEXT-JESCT(,R1)  JESCT EXTENSION
         MVC   SSOBSSIB,JESSMSIB-JESPEXT(R1) SMS SSIB
         LA    R5,SSSA_WRK             SSSA WORK AREA
         ST    R5,SSOBINDV             FUNCTION-DEPENDENT AREA
*---------------------------------------------------------------------*
         XC    SSSA_WRK,SSSA_WRK
         USING IEFSSSA,R5
         LA    R0,SSSALN+SSSA1LN+32    LENGTH
         STH   R0,SSSALEN
         MVC   SSSAID,=C'SSSA'         SSSA
         MVI   SSSAVER+1,SSOBSSVR      VERSION
         MVI   SSSASFN+1,SSSAACTV      SUB-FUNCTION CODE
         MVI   SSSAIFLG,SSSANAUT       NON-AUTHORIZED CALLER
         MVI   SSSA1TYP,SSSA1VOL       GIVE VOLSER, GET VLD BACK
         MVI   SSSA1CNT+3,1            ONE CONSTRUCT PASSED
         MVI   SSSA1NML+1,L'UCBVOLI    LENGTH OF NAME
         MVC   SSSA1NAM(L'UCBVOLI),UCBVOLI  MOVE VOLUME SERIAL
         LH    R0,=Y(VLDLEN)
         ST    R0,SSSA1LEN             LENGTH OF WORK AREA
         LA    R1,VLDAREA
         ST    R1,SSSA1PTR             ADDR OF WORK AREA
         LA    R1,SSOB_WRK             SSOB_PTR
         ST    R1,SSOB_ADR
         OI    SSOB_ADR,X'80'
         LA    R1,SSOB_ADR
         IEFSSREQ                  <== CALL SMS
         LTR   R15,R15                 SSI OK?
         BNZ   FINNOK                  NO, JUMP
         ICM   R6,B'1111',SSOBRETN
         LTR   R6,R6
         BZ    NEXT
         ABEND 001,DUMP
         DROP  R5,R6                   SMS_WORK,SSSA
*
NEXT     MVI   TIPO,C'S'
         LA    R9,VLDLEN
         A     R9,=F'71'
         B     DONE
NOSMS    MVI   TIPO,C'N'
         LA    R9,71
         B     DONE
OFFLINE  MVI   TIPO,C'F'
         LA    R9,11
         DROP  R3
DONE     ST    R9,STKLEN
         LA    R1,IRXSTKP
         LR    R15,R8
         BALR  R14,R15
         LTR   R15,15
         BNZ   FINNOK
*
         B     SCAN
ENDSCAN  EQU   *
         MODESET MODE=PROB,KEY=NZERO
         DELETE EP=IRXSTK
         LA    R8,1
         ST    R8,EVALBLOCK_EVLEN
         MVI   EVALBLOCK_EVDATA,C'1'
*---------------------------------
* EXIT
*---------------------------------
EXIT     SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
         LTORG
SAVER    DS    18F
F256     DC    F'256'
WORKA    DS    CL100
STATUS   DS    X
STATDEV  EQU   X'80'
STATSPA  EQU   X'40'
STATSMS  EQU   X'20'
INCOFF   EQU   X'08'
INCNSMS  EQU   X'04'
INCSMS   EQU   X'02'
MONITOR  EQU   X'01'
UCBPTR   DS    F
TIPO     DS    X
ADDR     DS    XL4
VOLSER   DS    CL6
RTNDAT   DS    6F
RTNDATL  DS    XL36
VLDAREA  DS    XL(VLDLEN)
VLDLEN   EQU   (VLDEND-VLD)+(VLDEND2-VLDSYSDT)
W1       DC    A(I)
W2       DC    A(N)
W3       DC    A(W)
W4       DC    A(T)
I        DS    F
N        DS    F
W        DS    F
T        DS    CL30
SSOB_ADR DS    F
KPULL    DC    CL8'PUSH    '
KQUEUE   DC    CL8'QUEUE   '
STKFUNC  DS    CL8
STKDAT   DS    F
STKLEN   DS    F
STKRC    DS    F
STKENV   DS    F
STKRCE   DS    F
IRXSTKP  DS    0F
P1       DC    A(STKFUNC)
P2       DC    A(STKDAT)
P3       DC    A(STKLEN)
P4       DC    A(STKRC)
P5       DC    A(STKENV)
P6       DC    A(STKRCE+X'80000000')
         DS    0D
SSOB_WRK DS    XL(SSOBHSIZ)            SSOB
         DS    0D
SSSA_WRK DS    XL(SSSALN+SSSA1LN+32)    SSSA (HDR)
*---------------------------------
* MAPS
*---------------------------------
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IEFUCBOB DEVCLAS=DA,PREFIX=YES
         IEFJSSOB
         IEFSSSA
         CVT DSECT=YES
         IEFJESCT
         IGDVLD
         END   IRXF@DAI
