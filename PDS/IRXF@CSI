IRXF@CSI TITLE 'REXX - CATALOG SEARCH INTERFACE'
IRXF@CSI CSECT
IRXF@CSI AMODE 31
IRXF@CSI RMODE ANY
*---------------------------------
* MACRO FOR CALLING PUTVREX
*---------------------------------
         MACRO
&NAME    $PUTV &REXXN,&ASMA,&ASML
&LREXXN  SETA  K'&REXXN-2
         MVC   VREXX_N(&LREXXN),=C&REXXN
         MVC   VREXX_NL,=F'&LREXXN'
         AIF   ('&ASMA'(1,1) NE '(').E1
         ST    &ASMA,VREXX_B
         AGO   .E2
.E1      LA    R1,&ASMA
         ST    R1,VREXX_B
.E2      AIF   ('&ASML'(1,1) NE '(').E3
         ST    &ASML,VREXX_BL
         AGO   .E4
.E3      LA    R1,&ASML
         ST    R1,VREXX_BL
.E4      BAL   R14,PUTVREX
         MEND
*---------------------------------
* HOUSEKEEPING
* R2  - EFPL
* R3  - ARG TABLE
* R4  - EVALBLOCK
* R8  - IRXEXCOM
* R9  - ENVBLOCK
* R12 - BASE
* R13 - SAVER AREA + WORKAREA
* R14 - MACROS + LINK
* R15 - MACROS + LINK
*---------------------------------
         @EQUS
         SAVE  (14,12),,*
         LR    R12,R15
         LR    R9,R0
         LR    R2,R1
         USING IRXF@CSI,R12
         GETMAIN RU,LV=LDATA,LOC=ANY
         LR    R15,R13
         LR    R13,R1
         ST    R15,4(R13)
         ST    R13,8(R15)
         SR    R15,R15
         USING DATA,R13
*---------------------------------
* MAIN
*---------------------------------
         USING ENVBLOCK,R9
         L     R8,ENVBLOCK_IRXEXTE
         USING IRXEXTE,R8
         L     R8,IRXEXCOM
         DROP  R8
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
         MVI   CONTINUE,C'1'
         BAL   R14,GETARGS
         BAL   R14,INITVAR
         BAL   R14,CALLCSI
*---------------------------------
* RETURN
*---------------------------------
RETOK    MVC   EVALBLOCK_EVDATA(1),CONTINUE          OK DONE
         LA    R1,1
         ST    R1,EVALBLOCK_EVLEN
         SLR   R11,R11
         B     RET
RETBAD   LA    R11,16                                BAD CALL
RET      LR    R1,R13
         L     R13,4(13)
         FREEMAIN RU,LV=LDATA,A=(1)
         LR    R15,R11
         RETURN (14,12),RC=(15)
*---------------------------------
* GETARGS
*---------------------------------
GETARGS  EQU   *
         ST    R14,GETARGSS
         LA    R5,SELCRITA
         USING CSIFIELD,R5
         MVC   CSIFILTK,=CL44' '
         MVC   CSICATNM,=CL44' '
         MVC   CSIRESNM,=CL44' '
         MVC   CSIDTYPD(16),=CL16' '
         MVC   CSIOPTS,=C'Y   '
         MVC   CSINUMEN,=H'0'
* GET ARG 1
         L     R6,ARGTABLE_ARGSTRING_PTR
         LTR   R6,R6
         BM    RETBAD                           NO FILTER SPEC.
         L     R7,ARGTABLE_ARGSTRING_LENGTH
         LTR   R7,R7
         BZ    RETBAD                           ZERO LENGTH FILTER
         C     R7,=F'44'                        > 44 ?
         BNH   COPFILT                          NO, COPY
         LA    R7,44                            YES, TRIM IT
COPFILT  BCTR  R7,R00
         EX    R7,COPFILTI                      COPY FILTER
         B     GETARG2
COPFILTI MVC   CSIFILTK(0),0(R6)
* GET ARG 2
GETARG2  LA    R3,ARGTABLE_NEXT
         L     R6,ARGTABLE_ARGSTRING_PTR
         LTR   R6,R6                            NO MORE ARGS ?
         BM    GETARGSR                         USE DEFAULTS
         L     R7,ARGTABLE_ARGSTRING_LENGTH
         LTR   R7,R7                            ZERO LENGTH ?
         BZ    GETARG3                          USE DEFAULT, GO NEXT
         C     R7,=F'44'                        > 44 ?
         BNH   COPCATN                          NO, COPY
         LA    R7,44                            YES, TRIM IT
COPCATN  BCTR  R7,R00
         EX    R7,COPCATNI                      COPY CATNAME
         B     GETARG3
COPCATNI MVC   CSICATNM(0),0(R6)
* GET ARG 3
GETARG3  LA    R3,ARGTABLE_NEXT
         L     R6,ARGTABLE_ARGSTRING_PTR
         LTR   R6,R6                            NO MORE ARGS ?
         BM    GETARGSR                         USE DEFAULTS
         L     R7,ARGTABLE_ARGSTRING_LENGTH
         LTR   R7,R7                            ZERO LENGTH ?
         BZ    GETARG4                          USE DEFAULT, GO NEXT
         C     R7,=F'16'                        > 16 ?
         BNH   COPTYPS                          NO, COPY
         LA    R7,16                            YES, TRIM IT
COPTYPS  BCTR  R7,R00
         EX    R7,COPTYPSI                      COPY TYPES
         B     GETARG4
COPTYPSI MVC   CSIDTYPD(0),0(R6)
* GET ARG 4
GETARG4  LA    R3,ARGTABLE_NEXT
         L     R6,ARGTABLE_ARGSTRING_PTR
         LTR   R6,R6                            NO MORE ARGS ?
         BM    GETARGSR                         USE DEFAULTS
         L     R7,ARGTABLE_ARGSTRING_LENGTH
         LTR   R7,R7                            ZERO LENGTH ?
         BZ    GETARG5                          USE DEFAULT, GO NEXT
         C     R7,=F'4'                         > 16 ?
         BNH   COPOPTS                          NO, COPY
         LA    R7,4                             YES, TRIM IT
COPOPTS  BCTR  R7,R00
         EX    R7,COPOPTSI                      COPY OPTIONS
         B     GETARG5
COPOPTSI MVC   CSIOPTS(0),0(R6)
* GET ARG 5
GETARG5  LA    R3,ARGTABLE_NEXT
         L     R6,ARGTABLE_ARGSTRING_PTR
         LTR   R6,R6                            NO MORE ARGS ?
         BM    GETARGSR                         USE DEFAULTS
         L     R7,ARGTABLE_ARGSTRING_LENGTH
         LTR   R7,R7                            ZERO LENGTH ?
         BZ    GETARGSR                         USE DEFAULT, GO NEXT
         LR    R1,R7                            USE TOTAL LENGTH
         LR    R15,R7
         LA    R0,CSIENTS
         LR    R14,R6
         MVCL  R0,R14                           COPY FIELD NAMES
         SRL   R7,3                             DIVIDE BY 8
         STH   R7,CSINUMEN                      SAVE ENTRIES NUMBER
GETARGSR L     R14,GETARGSS
         BR    R14
         DROP  R3,R5
*---------------------------------
* PUTVREX    *
*---------------------------------
* R2 - VAR_A *
PUTVREX  EQU   *
         ST    R14,PUTVREXS
         LA    R1,SHV1
         USING SHVBLOCK,R1
* OPT
         MVI   SHVCODE,SHVSTORE
         MVC   SHVNEXT,=F'0'
         MVC   SHVUSER,=F'0'
         MVC   SHVRET,=F'0'
* VAR
         LA    R15,VREXX_N
         ST    R15,SHVNAMA
         MVC   SHVNAML,VREXX_NL
         L     R15,VREXX_B
         ST    R15,SHVVALA
         MVC   SHVVALL,VREXX_BL
* CALL IRXEXCOM
         ST    R1,CP4
         DROP  R1
         LA    R1,VP1
         ST    R1,CP1
         LA    R1,VP2Y3
         ST    R1,CP2
         ST    R1,CP3
         OI    CP4,X'80'
         LA    R1,CP1
         LR    R0,R9
         LR    R15,R8
         BALR  R14,R15
         LTR   R15,R15
         BZ    SETREXR
         LR    R6,R15
         ABEND 001,DUMP
SETREXR  L     R14,PUTVREXS
         BR    R14
*---------------------------------
* CALLCSI
*---------------------------------
CALLCSI  EQU   *
         ST    R14,CALLCSIS
         GETMAIN RU,LV=BUFFCSI,LOC=ANY
         LR    R6,R1
         L     R1,=AL4(BUFFCSI)
         ST    R1,0(R6)
         LA    R1,REASONA
         ST    R1,W1
         LA    R1,SELCRITA
         ST    R1,W2
         LR    R1,R6
         ST    R1,W3
         OI    W3,X'80'
RESUME   LA    R1,W1
         LINK  EP=IGGCSI00
         C     R15,=F'8'
         BNL   CSIERR
         BAL   R14,EXTDATA
         LA    R1,SELCRITA
         USING CSIFIELD,R1
         CLI   CSIRESUM,C'Y'
         BE    RESUME
         DROP  R1
CSIEND   BAL   R14,WRTOTS
         LR    R1,R6
         FREEMAIN RU,LV=BUFFCSI,A=(1)
         L     R14,CALLCSIS
         BR    R14
CSIERR   MVI   CONTINUE,C'0'
         BAL   R14,DEBUG
         B     CSIEND
*---------------------------------
* EXTDATA
*---------------------------------
EXTDATA  EQU   *
         ST    R14,EXTDATAS
         L     R1,CSI_CALLS
         LA    R1,1(R1)
         ST    R1,CSI_CALLS
         USING CSIRWORK,R6
         LR    R7,R6
         A     R7,CSIUSDLN
         LA    R3,HEAD_END
         USING IGGCSI_ENTRY,R3
NENTRY   CR    R3,R7
         BNL   EXTDATAR
         CLI   CSIETYPE,X'F0'
         BE    CATENTRY
         LA    R1,=C'ENTRY '
         MVI   CMPNAMO,CMPNAMO_I+CMPNAMO_C
         BAL   R14,CMPNAM
         L     R1,CSI_ENTS
         LA    R1,1(R1)
         ST    R1,CSI_ENTS
         MVI   CMPNAMO,CMPNAMO_T+CMPNAMO_N
         BAL   R14,CMPNAM
         LA    R1,CSIENAME
         ST    R1,VREXX_B
         MVC   VREXX_BL,=F'44'
         BAL   R14,PUTVREX
         LA    R1,=C'FLAG '
         MVI   CMPNAMO,CMPNAMO_I+CMPNAMO_C
         BAL   R14,CMPNAM
         L     R1,CSI_ENTS
         MVI   CMPNAMO,CMPNAMO_T+CMPNAMO_N
         BAL   R14,CMPNAM
         LA    R1,CSIEFLAG
         ST    R1,VREXX_B
         MVC   VREXX_BL,=F'1'
         BAL   R14,PUTVREX
         TM    CSIEFLAG,CSIEDATA_F
         BZ    NODATA1
         BAL   R14,EXTFIEL
         B     VENTER
NODATA1  LA    R1,=C'ERROR '
         MVI   CMPNAMO,CMPNAMO_I+CMPNAMO_C
         BAL   R14,CMPNAM
         L     R1,CSI_ENTS
         MVI   CMPNAMO,CMPNAMO_C+CMPNAMO_N
         BAL   R14,CMPNAM
         LA    R1,CSIERETN
         ST    R1,VREXX_B
         MVC   VREXX_BL,=F'4'
         BAL   R14,PUTVREX
VENTER   TM    CSIEFLAG,CSIENTER
         BO    NODATA
         LH    R1,CSITOTLN
         B     NEXTRYP
NODATA   LA    R1,4
NEXTRYP  LA    R3,CSIEDATA
         AR    R3,R1
         B     NENTRY
CATENTRY EQU   *
         DROP  R3
         USING IGGCSI_CATALOG,R3
         LA    R1,=C'CATENTRY '
         MVI   CMPNAMO,CMPNAMO_I+CMPNAMO_C
         BAL   R14,CMPNAM
         L     R1,CSI_CENTS
         LA    R1,1(R1)
         ST    R1,CSI_CENTS
         MVI   CMPNAMO,CMPNAMO_T+CMPNAMO_N
         BAL   R14,CMPNAM
         LA    R1,CSICNAME
         ST    R1,VREXX_B
         MVC   VREXX_BL,=F'44'
         BAL   R14,PUTVREX
         LA    R3,CSICENTE
         B     NENTRY
EXTDATAR L     R14,EXTDATAS
         DROP  R6,R3
         BR    R14
*---------------------------------
* EXTFIEL
*---------------------------------
EXTFIEL  EQU   *
         ST    R14,EXTFIELS
         STM   R0,R15,SAVER2
         USING CSIRWORK,R6
         USING IGGCSI_ENTRY,R3
         LA    R1,SELCRITA
         USING CSIFIELD,R1
         LA    R7,CSIENTS
         DROP  R1
         LA    R5,CSILENF1
         LR    R11,R5
         LH    R1,CSINUMFD
         BCTR  R1,R0
         LR    R10,R1
         LTR   R10,R10
         BZ    EXTFIELR
         SLL   R1,1
         AR    R11,R1
NFIELD   EQU   *
         MVC   WORKF(8),0(R7)
         MVI   WORKF+8,C' '
         LA    R1,WORKF
         MVI   CMPNAMO,CMPNAMO_I+CMPNAMO_C
         BAL   R14,CMPNAM
         L     R1,CSI_ENTS
         MVI   CMPNAMO,CMPNAMO_C+CMPNAMO_N
         BAL   R14,CMPNAM
         LH    R1,0(R5)
         LTR   R1,R1
         BZ    FNOTF
         CH    R1,=H'-1'
         BE    FNOTA
         ST    R11,VREXX_B
         ST    R1,VREXX_BL
         AR    R11,R1
         B     WRVAR
FNOTA    LA    R1,=C'N/A'
         B     FNOTF1
FNOTF    LA    R1,=C'N/F'
FNOTF1   ST    R1,VREXX_B
         LA    R1,3
         ST    R1,VREXX_BL
WRVAR    BAL   R14,PUTVREX
SKFIELD  EQU   *
         LA    R7,8(R7)
         LA    R5,2(R5)
         BCT   R10,NFIELD
EXTFIELR LM    R0,R15,SAVER2
         L     R14,EXTFIELS
         BR    R14
*---------------------------------
* CMPNAM
*---------------------------------
CMPNAM   EQU   *
         ST    R14,CMPNAMS
         TM    CMPNAMO,CMPNAMO_I
         BZ    CMPNAM1
         MVC   VREXX_N(4),=CL4'CSI'
         MVC   VREXX_NL,=F'3'
CMPNAM1  LA    R15,VREXX_N
         L     R14,VREXX_NL
         AR    R15,R14
         MVI   0(R15),C'.'
         LA    R14,1(R14)
         ST    R14,VREXX_NL
         TM    CMPNAMO,CMPNAMO_N
         BO    CMPNAM3
         LA    R15,VREXX_N
         L     R14,VREXX_NL
         AR    R15,R14
NCHAR    CLI   0(R1),C' '
         BE    CMPNAM2
         MVC   0(1,R15),0(R1)
         LA    R1,1(R1)
         LA    R15,1(R15)
         LA    R14,1(R14)
         B     NCHAR
CMPNAM2  ST    R14,VREXX_NL
         B     CMPNAMR
CMPNAM3  CVD   R1,WORKD
         MVC   EDMSK(17),MASK
         ED    EDMSK(16),WORKD
         LA    R15,VREXX_N
         L     R14,VREXX_NL
         AR    R15,R14
         LA    R1,EDMSK
         LA    R0,16
NCHARN   CLI   0(R1),C' '
         BE    SKIPC
         MVC   0(1,R15),0(R1)
         LA    R15,1(R15)
         LA    R14,1(R14)
SKIPC    LA    R1,1(R1)
         BCT   R0,NCHARN
         ST    R14,VREXX_NL
CMPNAMR  L     R14,CMPNAMS
         BR    R14
*---------------------------------
* WRTOTS
*---------------------------------
WRTOTS   EQU   *
         ST    R14,WRTOTSS
         $PUTV 'CSI.DEBUG.CALLS',CSI_CALLS,4
         $PUTV 'CSI.ENTRY.0',CSI_ENTS,4
         $PUTV 'CSI.CATENTRY.0',CSI_CENTS,4
         L     R14,WRTOTSS
         BR    R14
*---------------------------------
* INITVAR
*---------------------------------
INITVAR  EQU   *
         ST    R14,INITVARS
         XC    CSI_CALLS,CSI_CALLS
         XC    CSI_ENTS,CSI_ENTS
         XC    CSI_CENTS,CSI_CENTS
         L     R14,INITVARS
         BR    R14
*---------------------------------
* DEBUG
*---------------------------------
DEBUG    EQU   *
         ST    R14,DEBUGS
         $PUTV 'CSI.DEBUG.RSN',REASONA,4
         $PUTV 'CSI.DEBUG.WA',(R6),256
         $PUTV 'CSI.DEBUG.FILTER',SELCRITA,L_CSIFIELD
         L     R14,DEBUGS
         BR    R14
*---------------------------------
* CONST AREA
*---------------------------------
VP1      DC    CL8'IRXEXCOM'
VP2Y3    DC    F'0'
MASK     DC    X'4020202020202020202020202020212040'
         LTORG
*---------------------------------
* DATA AREA
*---------------------------------
         DROP  R2,R4
DATA     DSECT
SAVER    DS    18F
SAVER2   DS    16F
GETARGSS DS    F
PUTVREXS DS    F
CALLCSIS DS    F
DEBUGS   DS    F
EXTDATAS DS    F
WRTOTSS  DS    F
INITVARS DS    F
CMPNAMS  DS    F
EXTFIELS DS    F
CONTINUE DS    C
WORKD    DS    D
WORKF    DS    CL9
EDMSK    DS    CL17
W1       DS    F
W2       DS    F
W3       DS    F
REASONA  DS    F
SHV1     DS    8F
CP1      DS    F
CP2      DS    F
CP3      DS    F
CP4      DS    F
VREXX_N  DS    CL64
VREXX_NL DS    F
VREXX_B  DS    F
VREXX_BL DS    F
CMPNAMO  DS    X
CMPNAMO_I  EQU X'80'               INITIALIZE
CMPNAMO_T  EQU X'00'               CONTINUE
CMPNAMO_N  EQU X'40'               NUMBER
CMPNAMO_C  EQU X'00'               CHAR
CSI_CALLS DS   F                   CALLS TO ICS (FIRST + RESUMES)
CSI_ENTS  DS   F                   ENTRIES READED
CSI_CENTS DS   F                   CATALOG ENTRIES READED
         DS    0D
SELCRITA DS    XL(L_CSIFIELD)
LDATA    EQU   *-DATA
L_CSIFIELD EQU (CSIENTS-CSIFIELD)+800
BUFFCSI  EQU   100*1024
*---------------------------------
* MAPS
*---------------------------------
MAPS     DSECT
         IRXENVB
         IRXEXTE
         IRXSHVB
         IRXEFPL
         IRXARGTB
         IRXEVALB
         COPY  IGGCSINA
         COPY  IGGCSIWK
         END   IRXF@CSI
