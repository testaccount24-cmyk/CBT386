**********************
* ESTABLECER DIRECC. *
**********************
IRXF@CA1 CSECT
IRXF@CA1 AMODE 31
IRXF@CA1 RMODE ANY
         @EQUS
ENTRY    TMMLINK (R12,R11)
         LR    R2,R1
         USING IRXF@CA1,R12
         ST    R0,STKENV
         GETMAIN RU,LV=340,LOC=(BELOW,ANY)
         LR    R10,R01
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
*******************
* LEER PARAMETRO 1*
*******************
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'6'
         BNE   FINNOK
         MVC   MYVOLSER(6),0(R5)
************************
* MAIN                 *
************************
         TMMOPTMC
         TMMGETVL MYVOLSER,(R10),NOTFOUND,ENQ=NO
         MVC   STKFUNC,KQUEUE
         LA    R1,340
         ST    R1,STKLEN
         LR    R1,R10
         ST    R1,STKDAT
         LA    R1,IRXSTKP
         LINK  EP=IRXSTK
         LTR   R15,15
         BNZ   FINNOK
         LA    R8,1
         ST    R8,EVALBLOCK_EVLEN
         MVI   EVALBLOCK_EVDATA,C'1'
         B     EXIT
NOTFOUND LA    R8,1
         ST    R8,EVALBLOCK_EVLEN
         MVI   EVALBLOCK_EVDATA,C'0'
************************
* EXIT                 *
************************
EXIT     FREEMAIN RU,A=((R10)),LV=340
         SR    R15,R15
         L     R13,SAVEAREA+4
         RETURN (14,12),,RC=0
FINNOK   FREEMAIN RU,A=((R10)),LV=340
         L     R13,SAVEAREA+4
         RETURN (14,12),,RC=16
ABEND001 ABEND 001
KQUEUE   DC    CL8'QUEUE   '
STKFUNC  DS    CL8
STKDAT   DS    F
STKLEN   DS    F
STKRC    DS    F
STKENV   DS    F
STKRCE   DS    F
IRXSTKP  DS    0F
P1       DC    A(STKFUNC)
P2       DC    A(STKDAT)
P3       DC    A(STKLEN)
P4       DC    A(STKRC)
P5       DC    A(STKENV)
P6       DC    A(STKRCE+X'80000000')
MYVOLSER DS    CL6
         TMMLINK PATCH
*************
* MAPAS     *
*************
D        DSECT
TMCREC   TMMTMREC DSECT=YES
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@CA1
