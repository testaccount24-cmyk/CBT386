**********************
* ESTABLECER DIRECC. *
**********************
IRXF@MOD CSECT
IRXF@MOD AMODE 31
IRXF@MOD RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R2,R1
         USING IRXF@MOD,R12
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
*******************
* LEER PARAMETRO 1*
*******************
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         LTR   R6,R6
         BZ    FINNOK
         C     R6,=F'4'
         BNE   FINNOK
         CLC   0(4,R5),=C'INIT'
         BE    HINIT
         CLC   0(4,R5),=C'GETM'
         BE    HGETM
         CLC   0(4,R5),=C'WAIT'
         BE    HWAIT
         CLC   0(4,R5),=C'TERM'
         BE    HTERM
         B     FINNOK
************************
* INIT QEDIT           *
************************
HINIT    LA    R7,EVALBLOCK_EVDATA
         EXTRACT (R7),FIELDS=COMM
         L     R7,EVALBLOCK_EVDATA
         USING IEZCOM,R7
         USING IEZCIB,R6
         L     R6,COMCIBPT
         QEDIT ORIGIN=COMCIBPT,BLOCK=(R6)
         QEDIT ORIGIN=COMCIBPT,CIBCTR=1
         LA    R9,4
         ST    R9,EVALBLOCK_EVLEN
         B     EXIT
         DROP  R6,R7
************************
* GET MODIFY           *
************************
* LEER PARAMETRO 2*
HGETM    LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'4'
         BNE   FINNOK
         L     R7,0(R5)
         USING IEZCOM,R7
         USING IEZCIB,R6
HWAITB   L     R6,COMCIBPT
         L     R8,COMECBPT
         TM    0(R8),X'40'
         BZ    NOMOD
         CLI   CIBVERB,CIBMODFY
         BNE   TSTSTOP
         LH    R8,CIBDATLN
         ST    R8,EVALBLOCK_EVLEN
         BCTR  R8,R0
         EX    R8,DATAMOVE
         B     BORRCMD
TSTSTOP  CLI   CIBVERB,CIBSTOP
         BNE   FINNOK
         MVC   EVALBLOCK_EVDATA(4),=C'STOP'
         LA    R9,4
         ST    R9,EVALBLOCK_EVLEN
BORRCMD  EQU   *
         QEDIT ORIGIN=COMCIBPT,BLOCK=(R6)
         B     EXIT
NOMOD    MVI   EVALBLOCK_EVDATA,C'0'
         LA    R9,1
         ST    R9,EVALBLOCK_EVLEN
         B     EXIT
DATAMOVE MVC   EVALBLOCK_EVDATA(0),CIBDATA
************************
* WAIT MODIFY          *
************************
HWAIT    LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'4'
         BNE   FINNOK
         L     R7,0(R5)
         USING IEZCOM,R7
         USING IEZCIB,R6
         L     R5,COMECBPT
         WAIT  ECB=(R5)
         L     R6,COMCIBPT
         B     HWAITB
************************
* TERM QEDIT           *
************************
HTERM    LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'4'
         BNE   FINNOK
         L     R7,0(R5)
         USING IEZCOM,R7
         USING IEZCIB,R6
         L     R6,COMCIBPT
         QEDIT ORIGIN=COMCIBPT,BLOCK=(R6)
         QEDIT ORIGIN=COMCIBPT,CIBCTR=0
         MVI   EVALBLOCK_EVDATA,C'1'
         LA    R9,1
         ST    R9,EVALBLOCK_EVLEN
************************
* EXIT                 *
************************
EXIT     SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
IEZCOM   DSECT
         IEZCOM
IEZCIB   DSECT
         IEZCIB
         END   IRXF@MOD
