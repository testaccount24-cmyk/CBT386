**********************
* ESTABLECER DIRECC. *
**********************
IRXF@VST CSECT
IRXF@VST AMODE 31
IRXF@VST RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R2,R1
         USING IRXF@VST,R12
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
*******************
* LEER PARAMETRO 1*
*******************
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'8'
         BH    FINNOK
         LTR   R6,R6
         BZ    FINNOK
*******************
* MOVER DATOS     *
*******************
         MVC   PREFIX,=CL8' '
         BCTR  R6,R0
         EX    R6,MOVESTC
*******************
* BUSCAR STC      *
*******************
         USING PSA,0
         L     R10,FLCCVT
         L     R10,CVTASVT-CVTMAP(,R10)
         L     R6,ASVTMAXU-ASVT(,R10)
         LA    R10,ASVTENTY-ASVT(,R10)
LOOPASCB EQU   *
         TM    0(R10),ASVTAVAI
         BO    NEXTASCB
         L     R5,0(R10)
         LA    R5,0(R5)
         LTR   R5,R5
         BZ    NEXTASCB
         USING ASCB,R5
         L     R7,ASCBOUCB
         LA    R7,0(R7)
         LTR   R7,R7
         BZ    NEXTASCB
         USING OUCB,R7
         TM    OUCBSFL,OUCBINV
         BO    NEXTASCB
         CLC   ASCBJBNI,=F'0'
         BE    STSTC
         L     R8,ASCBJBNI
         MVC   NAME,0(R8)
         B     NAMESET
STSTC    L     R8,ASCBJBNS
         MVC   NAME,0(R8)
NAMESET  CALL  UTILMATC,(NAME,PREFIX,LLNGSTR,LLNGPAT)
         LTR   R15,R15
         BZ    EXIT
NEXTASCB EQU   *
         LA    R10,4(R10)
         BCT   R6,LOOPASCB
         MVI   EVALBLOCK_EVDATA,C'0'
         B     EXITM
************************
* EXIT                 *
************************
EXIT     MVI   EVALBLOCK_EVDATA,C'1'
EXITM    LA    R9,1
         ST    R9,EVALBLOCK_EVLEN
         SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVESTC  MVC   PREFIX(0),0(R5)
NAME     DS    CL8
PREFIX   DS    CL8
LLNGSTR  DC    X'08'
LLNGPAT  DC    X'08'
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IRAOUCB DSECT=YES
         IHAASCB DSECT=YES
         IHAASVT LIST=NO
         CVT   DSECT=YES,LIST=NO
         IHAPSA LIST=NO
         END   IRXF@VST
