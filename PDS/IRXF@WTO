**********************
* ESTABLECER DIRECC. *
**********************
IRXF@WTO CSECT
IRXF@WTO AMODE 31
IRXF@WTO RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R2,R1
         USING IRXF@WTO,R12
         USING EFPL,R2
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING EVALBLOCK,R4
         MVI   TYPE,C'N'
         MVI   SUPS,C'N'
*******************
* LEER PARAMETRO 1*
*******************
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         LTR   R6,R6
         BZ    FINNOK
         C     R6,F122
         BNH   MOVTWK
         LA    R6,122
MOVTWK   STH   R6,WTOTEXT
         BCTR  R6,R0
         EX    R6,MOVWT
*******************
* LEER PARAMETRO 2*
*******************
         LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    EXECUTE
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         LTR   R6,R6
         BZ    RP3
         CLI   0(R5),C'R'
         BNE   LP2
         MVI   TYPE,C'R'
         B     RP3
LP2      CLI   0(R5),C'B'
         BNE   RP3
         MVI   TYPE,C'B'
*******************
* LEER PARAMETRO 3*
*******************
RP3      LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    EXECUTE
         CLI   0(R5),C'S'
         BNE   EXECUTE
         MVI   SUPS,C'S'
*******************
* EXECUTE         *
*******************
EXECUTE  CLI   SUPS,C'S'
         BNE   EXECUT2
         LA    R0,1
         LA    R15,200
         SVC   109
EXECUT2  CLI   TYPE,C'N'
         BE    HWTO
         CLI   TYPE,C'R'
         BE    HWTOR
************************
* HACER WTO BLANCO     *
************************
HWTOB    MVC   HWTOB1+32(2),=B'0100000000000000'
HWTOB1   WTO   TEXT=WTOTEXT
         LTR   R15,R15
         BNZ   FINNOK
         ST    R1,EVALBLOCK_EVDATA
         LA    R9,4
         ST    R9,EVALBLOCK_EVLEN
         B     EXIT
************************
* HACER WTOR           *
************************
HWTOR    XC    ECBREPLY,ECBREPLY
         MVI   REPLY,C' '
         MVC   REPLY+1(118),REPLY
         WTOR  TEXT=(WTOTEXT,REPLY,119,ECBREPLY)
         LTR   R15,R15
         BNZ   FINNOK
         WAIT  ECB=ECBREPLY
         MVC   EVALBLOCK_EVDATA(119),REPLY
         LA    R9,119
         LA    R5,EVALBLOCK_EVDATA+118
STRIPT   CLI   0(R5),C' '
         BNE   SETL
         BCTR  R5,R0
         BCT   R9,STRIPT
SETL     LTR   R9,R9
         BNZ   SETL2
         LA    R9,1
SETL2    ST    R9,EVALBLOCK_EVLEN
         B     EXIT
************************
* HACER WTO            *
************************
HWTO     WTO   TEXT=WTOTEXT
         LTR   R15,R15
         BNZ   FINNOK
         MVI   EVALBLOCK_EVDATA,C'1'
         LA    R9,1
         ST    R9,EVALBLOCK_EVLEN
************************
* EXIT                 *
************************
EXIT     CLI   SUPS,C'S'
         BNE   EXIT1
         LA    R0,2
         LA    R15,200
         SVC   109
EXIT1    SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVWT    MVC   WTOTEXT+2(0),0(R5)
F122     DC    F'122'
WTOTEXT  DS    CL124
REPLY    DS    CL119
ECBREPLY DS    F
TYPE     DS    C
SUPS     DS    C
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@WTO
