IRXFMAT2 TITLE 'MATCH Wildcard source/pattern Rexx Function'
IRXFMAT2 RSECT
IRXFMAT2 AMODE 31
IRXFMAT2 RMODE ANY
         YREGS
*----------------------------------------------------------------------
* HOUSEKEEPING
*        R15 - Work
*        R14 - Rexx EvalBlock
*        R13 - Saver chain
*        R12 - Base
*        R11 - Rexx EFPL
*        R10 - Rexx EFPLARG
*        R9  - Work
*        R8  - Work
*        R7  - MP
*        R6  - CP
*        R5  - Pattern
*        R4  - Pattern length
*        R3  - Source
*        R2  - Source Length
*        R1  - n/a
*        R0  - n/a
*----------------------------------------------------------------------
         SAVE  (14,12),,'IRXFMAT2 MATCH SOURCE/PATTERN'
         LR    R12,R15
         USING IRXFMAT2,R12
         LR    R11,R1
         USING EFPL,R11
         L     R10,EFPLARG
         USING ARGTABLE_ENTRY,R10
         L     R14,EFPLEVAL
         L     R14,0(R14)
         USING EVALBLOCK,R14
*----------------------------------------------------------------------
* arg(1) Source
*----------------------------------------------------------------------
         L     R3,ARGTABLE_ARGSTRING_PTR           Source Pointer
         LTR   R3,R3
         JM    EXITBAD
         L     R2,ARGTABLE_ARGSTRING_LENGTH        Source length
*----------------------------------------------------------------------
* arg(2) Pattern
*----------------------------------------------------------------------
         LA    R10,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR           Pattern Pointer
         LTR   R5,R5
         JM    EXITBAD
         L     R4,ARGTABLE_ARGSTRING_LENGTH        Pattern length
*-Pseudocode-----------------------------------------------------------
* ARG STRING,PATTERN
* WILD = 1
* STR = 1
* CP = 1
* MP = 1
*----------------------------------------------------------------------
         LR    R6,R3               CP
         LR    R7,R5               MP
         LR    R8,R3
         AR    R8,R2               END OF STRING
         LR    R9,R5
         AR    R9,R4               END OF PATTERN
*-Pseudocode-----------------------------------------------------------
* DO WHILE(STR <= LENGTH(STRING) & SUBSTR(PATTERN,WILD,1) *= '*')
*    IF SUBSTR(PATTERN,WILD,1) *= SUBSTR(STRING,STR,1) &,
*       SUBSTR(PATTERN,WILD,1) *= '%' THEN RETURN -1
*    WILD = WILD + 1
*    STR = STR + 1
*    END
*----------------------------------------------------------------------
LOOP1    CR    R3,R8
         JE    LOOP2
         CLI   0(R5),C'*'
         JE    LOOP2
         CLC   0(1,R5),0(R3)
         JE    LOOP1C
         CLI   0(R5),C'%'
         JNE   FALSE
LOOP1C   LA    R5,1(R5)
         LA    R3,1(R3)
         CR    R5,R9
         JNE   LOOP1
         CR    R3,R8
         JE    TRUE
         J     FALSE
*-Pseudocode-----------------------------------------------------------
* DO WHILE(STR <= LENGTH(STRING))
*----------------------------------------------------------------------
LOOP2    CR    R3,R8
         JE    LOOP3
*-Pseudocode-----------------------------------------------------------
*   IF SUBSTR(PATTERN,WILD,1) = '*' THEN DO
*      WILD = WILD+1
*      IF WILD > LENGTH(PATTERN) THEN RETURN 1
*      MP = WILD
*      CP = STR+1
*      END
*----------------------------------------------------------------------
         CLI   0(R5),C'*'
         JNE   LOOP21
         LA    R5,1(R5)
         CR    R5,R9
         JE    TRUE
         LR    R7,R5
         LA    R6,1(R3)
         J     LOOP2
*-Pseudocode-----------------------------------------------------------
*   ELSE IF SUBSTR(PATTERN,WILD,1)=SUBSTR(STRING,STR,1) |,
*           SUBSTR(PATTERN,WILD,1)='%' THEN DO
*        WILD = WILD + 1
*        STR = STR + 1
*        END
*----------------------------------------------------------------------
LOOP21   CLC   0(1,R5),0(R3)
         JE    LOOP211
         CLI   0(R5),C'%'
         JNE   LOOP22
LOOP211  LA    R5,1(R5)
         LA    R3,1(R3)
         J     LOOP2
*-Pseudocode-----------------------------------------------------------
*   ELSE DO
*      WILD = MP
*      CP = CP+1
*      STR = CP
*      END
*   END
*----------------------------------------------------------------------
LOOP22   LR    R5,R7
         LA    R6,1(R6)
         LR    R3,R6
         J     LOOP2
*-Pseudocode-----------------------------------------------------------
* DO WHILE(SUBSTR(PATTERN,WILD,1)='*')
*    WILD = WILD + 1
*    END
*----------------------------------------------------------------------
LOOP3    CLI   0(R5),C'*'
         JNE   END
         LA    R5,1(R5)
         J     LOOP3
*-Pseudocode-----------------------------------------------------------
* IF WILD > LENGTH(PATTERN) THEN RETURN 1
* ELSE RETURN -1
*----------------------------------------------------------------------
END      CR    R5,R9
         JNL   TRUE
FALSE    MVI   EVALBLOCK_EVDATA,C'0'
         J     RETURN
TRUE     MVI   EVALBLOCK_EVDATA,C'1'
RETURN   MVC   EVALBLOCK_EVLEN,=F'1'
         SR    R15,R15
RETURN2  ST    R15,16(R13)
         LM    R14,R12,12(R13)
         BSM   0,R14
EXITBAD  LA    R15,16
         J     RETURN2
*----------------------------------------------------------------------
* Used Maps
*----------------------------------------------------------------------
DUMMY    DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXFMAT2
